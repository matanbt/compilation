/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;

parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print(":");
		System.out.print(lexer.getCharPos());
		System.out.print("] ");		
		System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	// TODO check if ERROR and act accordingly
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print(":");
	System.out.print(lexer.getCharPos());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	if (s.value != null)
	{
		System.out.print("( ");
		System.out.print(s.value);
		System.out.print(" )");
	}
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
/* Keywords */
terminal CLASS;
terminal NIL;
terminal ARRAY;
terminal WHILE;
terminal TYPE_INT;
terminal EXTENDS;
terminal RETURN;
terminal NEW;
terminal IF;
terminal TYPE_STRING;
terminal TYPE_VOID;

/* chars */
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal COMMA;
terminal DOT;
terminal SEMICOLON;
terminal ASSIGN;
terminal EQ;
terminal LT;
terminal GT;

/* Error - special terminal for lexical error */
terminal ERROR;

/*************/
/* TERMINALS with value */
/*************/
terminal String INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_STMT_LIST stmtList;
non terminal AST_DEC_FUNC funcDec;
non terminal AST_DEC_FUNC_ARG_LIST funcArgs;
non terminal AST_EXP_LIST expList;
non terminal program;
non terminal AST_TYPE type;
non terminal decList;
non terminal dec;
non terminal AST_DEC_VAR varDec;
non terminal AST_NEW_EXP newExp;
non terminal String binop;
non terminal AST_CLASSDEC classDec;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_CFIELD cField;
non terminal AST_ARRAY_TYPE_DEF arrayTypedef;


/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;


/************************/
/* START WITH: stmtList */
/************************/
start with program;

/********************/
/* DERIVATION RULES */
/********************/

program     ::=     decList;

type        ::=     TYPE_INT                  {: RESULT = new AST_TYPE("int"); :}
                    | TYPE_STRING             {: RESULT = new AST_TYPE("string"); :}
                    | TYPE_VOID               {: RESULT = new AST_TYPE("void"); :}
                    | ID: customizedType      {: RESULT = new AST_TYPE(customizedType); :}
                    ;

decList     ::=     dec:s   decList:l
                    | dec:s
                    ;

dec         ::=     varDec:s
                    | funcDec: s
                    | classDec:s
                    | arrayTypedef:s
                    ;

//---------------- variable declaration ----------------
varDec      ::=     type:t ID:i SEMICOLON                       {: RESULT = new AST_DEC_VAR(t, i); :}
                    | type:t ID:i ASSIGN exp:e SEMICOLON        {: RESULT = new AST_DEC_VAR(t, i, e); :}
                    | type:t ID:i ASSIGN newExp:ne SEMICOLON    {: RESULT = new AST_DEC_VAR(t, i, ne); :}
                    ;
/** arrayDef **/
arrayTypedef    ::=     ARRAY ID:i EQ type:t LBRACK RBRACK SEMICOLON   {: RESULT = new AST_ARRAY_TYPE_DEF(t, i);    :};

/** classDec **/

classDec    ::=     CLASS ID EXTENDS ID:name LBRACE cFieldList:l RBRACE      {: RESULT = new AST_CLASSDEC(l,name);    :}
                    |  CLASS ID LBRACE cFieldList:l RBRACE                   {: RESULT = new AST_CLASSDEC(l,null);    :}
                    ;

cFieldList	::=		cField:s	cFieldList:l		        {: RESULT = new AST_CFIELD_LIST(s,l);    :}
					| cField:s				                {: RESULT = new AST_CFIELD_LIST(s,null); :}
					;

cField    ::=       varDec:varDec                                   {: RESULT = new AST_CFIELD_VAR(varDec);   :}
                    | funcDec:funcDec                               {: RESULT = new AST_CFIELD_FUNC(funcDec); :}
                    ;


var			::=		ID:name								{: RESULT = new AST_VAR_SIMPLE(name);       :}
					| var:v DOT ID:fieldName			{: RESULT = new AST_VAR_FIELD(v,fieldName); :}
					| var:v LBRACK exp:e RBRACK			{: RESULT = new AST_VAR_SUBSCRIPT(v,e);     :}
					;

//---------------- statements list ----------------
stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AST_STMT_LIST(s,l);    :}
					| stmt:s				{: RESULT = new AST_STMT_LIST(s,null); :}
					;

stmt		::=		varDec
                    | var:v ASSIGN exp:e SEMICOLON								{: RESULT = new AST_STMT_ASSIGN(v, e); :}
                    | var:v ASSIGN newExp:e SEMICOLON                           {: RESULT = new AST_STMT_ASSIGN_NEW(v, e); :}
                    | RETURN SEMICOLON                                          {: RESULT = new AST_STMT_RETURN(); :}
                    | RETURN exp:e SEMICOLON                                    {: RESULT = new AST_STMT_RETURN(e); :}
					| IF    LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_IF(cond, body); :}
					| WHILE LPAREN exp:cond RPAREN LBRACE stmtList:body RBRACE	{: RESULT = new AST_STMT_WHILE(cond, body); :}
					| ID:idName LPAREN RPAREN SEMICOLON                               {: RESULT = new AST_STMT_CALL(idName); :}
					| ID:idName LPAREN expList:eList RPAREN SEMICOLON                 {: RESULT = new AST_STMT_CALL(idName, eList); :}
					| var:v DOT ID:idName LPAREN LBRACE SEMICOLON                     {: RESULT = new AST_STMT_CALL(v, idName); :}
					| var:v DOT ID:idName LPAREN expList:eList RPAREN SEMICOLON       {: RESULT = new AST_STMT_CALL(v, idName, eList); :}
					;

// ---------------- Function Declaration ----------------
funcDec    ::=    type:rtnType ID:funcName LPAREN RPAREN LBRACE stmtList:body RBRACE                    {: RESULT = new AST_DEC_FUNC(rtnType, funcName, body); :}
                | type:rtnType ID:funcName LPAREN funcArgs: argList RPAREN LBRACE stmtList:body RBRACE  {: RESULT = new AST_DEC_FUNC(rtnType, funcName, argList, body); :}
                ;

// Function arguments (one or more)
funcArgs   ::=   type:argType ID:argName COMMA funcArgs:lst    {: RESULT = new AST_DEC_FUNC_ARG_LIST(new AST_DEC_FUNC_ARG(argType, argName), lst); :}
                | type:argType ID:argName                      {: RESULT = new AST_DEC_FUNC_ARG_LIST(new AST_DEC_FUNC_ARG(argType, argName), null); :}
                ;


exp         ::=     var:v								        {: RESULT = new AST_EXP_VAR(v); :}
                    | LPAREN exp:e RPAREN                       {: RESULT = e; :}
                    | exp:e1 binop:op exp:e2                    {: RESULT = new AST_EXP_BINOP(e1, e2, op); :}
                    | var:v DOT ID:i LPAREN expList:l RPAREN    {: RESULT = new AST_EXP_CALL(v, i, l); :}
                    | var:v DOT ID:i LPAREN RPAREN              {: RESULT = new AST_EXP_CALL(v, i); :}
                    | ID:i LPAREN expList:l RPAREN              {: RESULT = new AST_EXP_CALL(i, l); :}
                    | ID:i LPAREN RPAREN                        {: RESULT = new AST_EXP_CALL(i); :}
                    | MINUS INT:i                               {: RESULT = new AST_EXP_INT(-Integer.parseInt(i)); :}
                    | INT:i                                     {: RESULT = new AST_EXP_INT(Integer.parseInt(i)); :}
                    | NIL                                       {: RESULT = new AST_EXP_NIL(); :}
                    | STRING:s                                  {: RESULT = new AST_EXP_STRING(s); :}
                    ;

newExp      ::=     NEW ID:idName                           {: RESULT = new AST_NEW_EXP_SIMPLE(idName); :}
                    | NEW ID:idName LBRACK exp:e RBRACK     {: RESULT = new AST_NEW_EXP_IDX(idName, e); :}
                    ;

// list of one or more exp
expList	::=		exp:e COMMA expList:lst {: RESULT = new AST_EXP_LIST(e, lst); :}
                | exp:e                 {: RESULT = new AST_EXP_LIST(e, null); :}
                ;

// ---------------- Binary Operations ----------------
binop       ::=     PLUS          {: RESULT = "+"; :} // we'll parse each operator as its string (TODO - we can also create a new class AST_OPERATOR, i think it's redundant)
                    | MINUS       {: RESULT = "-"; :}
                    | TIMES       {: RESULT = "*"; :}
                    | DIVIDE      {: RESULT = "/"; :}
                    | LT          {: RESULT = "<"; :}
                    | GT          {: RESULT = ">"; :}
                    | EQ          {: RESULT = "="; :}
                    ;
